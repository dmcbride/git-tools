#!/usr/bin/perl

use 5.010;

# git-swj - switch git branch using jira issue key.

my $jira_prefix = `git config --get jira.prefix`;
chomp $jira_prefix;

if ($jira_prefix eq "") {
    say "No jira prefix set. Please set it using:";
    say "git config jira.prefix <jira_prefix>";
    exit 1;
}

my $number = shift @ARGV;
if ($number eq "") {
    say "No issue number provided. Please provide an issue number.";
    exit 1;
}

if ($number !~ /^\d+$/) {
    say qq[Invalid issue number. Please provide a valid issue number. (Exclude the prefix, "$jira_prefix-")];
    exit 1;
}

my $branch_name_match = @ARGV ? join("-", @ARGV) : undef;

# find a branch name that starts with the jira prefix and issue number, and, if we have a branch name match, also matches that
my @branch_names = get_branches_matching("$jira_prefix-$number-*");

@branch_names = grep /$branch_name_match/, @branch_names if $branch_name_match; # filter by branch name match

if (@branch_names == 0) {
    if (!defined $branch_name_match) {
        # maybe we have a branch that starts with the jira prefix and issue number, but nothing beyond that.
        @branch_names = get_branches_matching("$jira_prefix-$number");
        if (@branch_names == 1) {
            say "Switching to branch: $branch_names[0]";
            system(qw(git switch), $branch_names[0]) == 0
                or die "Failed to switch branch: $!";
            exit 0;
        }
    }

    say "No matching branches found.";
    exit 1;
} elsif (@branch_names == 1) {
    say "Switching to branch: $branch_names[0]";
    system(qw(git switch), $branch_names[0]) == 0
        or die "Failed to switch branch: $!";
} else {
    say "Multiple branches found:";
    say join("\n", @branch_names);
    exit 1;
}

sub get_branches_matching
{
    my $prefix = shift;

    my @branches = `git branch --list "$prefix"`;
    chomp @branches;

    @branches = map { s/^[+*]?\s*//r } @branches; # remove leading whitespace
    @branches = map { s/\s*$//r } @branches; # remove trailing whitespace

    return @branches;
}